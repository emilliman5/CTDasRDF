PREFIX study: <http://example.org/study#>
PREFIX sdtmc: <http://rdf.cdisc.org/ct/schema#>
SELECT ?study ?domain ?subjid ?usubjid ?age ?sex
FROM <http://localhost:8890/SDEDEC2016>
WHERE {
    ?person a                study:EnrolledSubject ;
    study:hasUniqueSubjectID ?usubjid_ ;
    study:hasSubjectID       ?subjid_ ;
    study:hasAgeMeasurement  ?agemeas ;
    study:hasSex             ?sexcode_ .

    ?sexcode_ sdtmc:cdiscSubmissionValue ?sex_ .
    ?agemeas study:hasActivityOutcome ?ageOutcome.
    ?ageOutcome  study:hasValue  ?age_.

    # Make so pretty-pretty
    bind( str(?subjid_) as ?subjid  )
    bind( str(?usubjid_) as ?usubjid  )
    bind( xsd:integer(?age_) as ?age )
    bind( str(?sex_) as ?sex  )
    
    # Cheating here. Pay no attention to that man behind the curtain
    bind("DM" as ?domain)
    bind("CDISCPILOT01" as ?study)
}